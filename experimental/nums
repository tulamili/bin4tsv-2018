#!/usr/bin/perl
use 5.014 ; use strict ; use warnings ;
use Getopt::Std ; getopts 'c:CS,:', \my%o ;
$o{','} //= "\n" ; # 数を出力するときの、区切り文字の指定
$o{c} //= '' ; # -Cでgawkコマンド分を出力するときの、awk条件節の分の指定

my @arg = &expandArg ( @ARGV ) ;
&OperationC and exit 0 if $o{C} ;
&OperationS and exit 0 if $o{S} ;
&OperationN and exit 0 ; #if $o{n} ;

exit 0 ;

sub OperationC {
    my $out = '' ;
    $out .= q[ gawk ] ;
    $out .= q[ -F\t -vOFS=\t ] ;
    $out .= $o{c} ;
    $out .= q[{print] ;
     # -Cで動作させて、bashでエラーにならないようにするため、
     # print の後ろに空白文字は入れてはいけないようだ。
    $out .= join ',' , map { '$'.$_ } @arg ;
    $out .= q[}] ;
    print $out ;
}

sub OperationS {
    my $out = '' ;
    $out .= q[ gawk ] ;
    $out .= q[ -F\t -vOFS=\t ] ;
    $out .= $o{c} ;
    $out .= q[{] ;
    $out .= join ';' , map { "s$_+=\$$_" } @arg ;
    $out .= q[}] ;
    $out .= q[END{print(] ;
    $out .= join ',' , map { "s$_" } @arg ;
    $out .= q[)}] ;
    print $out ;
}


sub expandArg (@) {

    # コンマ区切りの展開
    for ( reverse 0..$#_ ) {
        splice @_ , $_ , 1 , split /,/ , $_[$_] , 0 ;
    }

    # ".."  の展開
    for ( reverse 0..$#_ ) {
        #if ( $_[$_] =~ /^(-?\d+)\.\.(-?\d+)$/ ) {

        if ( $_[$_] =~ /^(.*)\.\.(.*)$/ ) {
            my ( $n1 , $n2 ) = ( $1, $2 ) ;
            my $reg = qr/^([+-]?)(?=[0-9]|\.[0-9])[0-9]*(\.[0-9]*)?([Ee]([+-]?[0-9]+))?$/ ;
            $n1 !~ $reg || $n2 !~ $reg and next ;
            my @tmp =  $n1 <= $n2 ?  $n1..$n2 : reverse $n2..$n1  ;
            splice @_ , $_ , 1 , @tmp ;
        }
    }
    # ちょっとした後処理
    @_ = grep {$_ ne ''} @_ ;
    return @_
}


sub OperationN {
    my $sep = eval qq{qq{$o{','}}}  ;
    print join $sep, @arg ;
    print "\n" ;
    1 ;
}


sub VERSION_MESSAGE{}
sub HELP_MESSAGE{
    $0 =~ s|.*/|| ;
    while ( <DATA> ) {
        s/\$0/$0/g ;
        print $_ if s/^=head1// ..s/^=cut// ;
    }
    exit 0 ;
}

__END__

=encoding utf8

=head1
 $0 arg

    arg で指定される数を出力する。
    -C により、指定された列のみを取り出す、gawkのコマンド文を生成する。

 オプション:

    -, char  : 数を出力するときに、区切り文字を改行文字から変更する。
               例: -, \t 　でタブ文字を指定。
                   -,,   コンマ文字を指定。
                   -, '' 区切り文字は0文字、つまり、何もはさまないで数を並べて出力。(-, と ''　の間は必ず空白を入れる。)

    -C  : gawkのコマンド文を出力する。どの列を出力するかは、argにより指定される。
    -S  : 指定された各列の列和を求めるgawkのコマンド文を出力する。
    -c  'str' : gawk文のコマンド文に 条件節を追加する。-Cまたは-Sと共に用いる。


 使用例1 (単に数のリストを出力する場合)

     $0 5..10   # ->  5から10までの数を改行区切りで出力。
     $0 5,3 2,1  # -> 5,3,2,1を改行区切りで出力。空白区切りもコンマ区切りも同じ区切りと見なす。
     $0 10..5 -3  # -> 10,9,8,7,6,5 と -3 を出力。

 使用例2 (gawkコマンド文を出力する場合)

    `$0 -C 5..10, 1 ` file

      # バッククォーテーションで囲まれている部分が、gawk のコマンドに置き換わる。
      # この場合は、gawk -F\t -vOFS=\t {print$5,$6,$7,$8,$9,$10,$1} となる。

    `$0 -c'$1==10' -C 1..3 `
     $( $0 -c '$1==10' -C 1..3 )

       # 1列目が値10であるときのみ、1列目から3列目を出力する。
       # 条件節を表す文字列は必ず ''で囲うこと。シェルが$1などを解釈してしまうため
       # 条件節に  || や && を使うときは、前後に空白を入れないこと。

  使用例3 (列を選択して、その各列の和を求めるためのgawkコマンド文を生成)
     `$0 -S 3 `    # 3番目の列の和を求める


 注意点:
    引数に与えられた数は整数として扱われやすい。Perlの .. 演算子を使っているため。
=cut
